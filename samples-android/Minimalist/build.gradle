apply plugin: 'com.android.model.application'
// Retrieve ndk path: ndk.dir MUST be set in file local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')

def gpg_cpp_path = file(project(':Common/gpg-sdk').projectDir).absolutePath + "/gpg-cpp-sdk/android"

// stlport configuration ["c++_static", "c++_shared", "gnustl_static", "gnustl_shared"]
// used to set stl and location of libgpg.a
def stlportType = "c++"
def stllinkType = "static"

model {
    repositories {
        libs(PrebuiltLibraries) {
            gpg {
                headers.srcDir "${gpg_cpp_path}/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("${gpg_cpp_path}/lib/${stlportType}/${targetPlatform.getName()}/libgpg.a")
                }
            }
        }
    }

    android {
        compileSdkVersion = 22
        buildToolsVersion = "23.0.1"

        defaultConfig.with {
            applicationId = "com.google.example.games.Minimalist"
            minSdkVersion.apiLevel = 11
            targetSdkVersion.apiLevel = 10

        }
    }

    android.sources {
        main {
            jni {
                dependencies {
                    library "gpg" linkage "static"
                    project ":Common/native_app_glue" linkage "static"
                }
                source {
                    srcDir 'src/main/jni'
                }
            }
        }
    }

    android.ndk {
        platformVersion = 11
        moduleName = "native-activity"
        stl = "${stlportType}_${stllinkType}"
        ldLibs.addAll(["atomic", "log", "android", "EGL", "GLESv1_CM", "GLESv2", "z"])
        abiFilters.addAll(["armeabi", "armeabi-v7a", "x86", "arm64-v8a"])
        cppFlags.addAll(["-std=c++11", "-I${file("src/main")}".toString()])
    }
    android.abis {
        create("armeabi-v7a") {
            cppFlags.addAll(["-mhard-float", "-D_NDK_MATH_NO_SOFTFP=1", "-mfloat-abi=hard"])
            ldLibs.add("m_hard")
            ldFlags.add("-Wl,--no-warn-mismatch")
        }
    }

    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles.add(file('proguard-android.txt'))
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.google.android.gms:play-services:8.3.0'
}
